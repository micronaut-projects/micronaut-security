plugins {
    id 'java-library'
}
repositories {
    mavenCentral()
}
dependencies {
    testAnnotationProcessor(platform(mn.micronaut.bom))
    testAnnotationProcessor(mn.micronaut.inject.java)

    testImplementation(platform(mn.micronaut.bom))
    testImplementation(libs.junit.jupiter.api)
    testImplementation(mn.micronaut.test.junit5)
    testRuntimeOnly(libs.junit.jupiter.engine)

    testRuntimeOnly(libs.logback.classic)
    testImplementation(mn.micronaut.management)
    testImplementation("io.micronaut.servlet:micronaut-http-server-tomcat")
    testImplementation(mn.micronaut.http.client)
    testImplementation project(":security-jwt")
    testImplementation project(":test-suite-utils-security")
    testImplementation(libs.reactor.core)
}

tasks.named('test') {
    useJUnitPlatform()
}

apply from: "${rootProject.projectDir}/gradle/testVerbose.gradle"

java {
    sourceCompatibility = JavaVersion.toVersion("1.8")
    targetCompatibility = JavaVersion.toVersion("1.8")
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'io.micronaut.servlet') {
            details.useVersion "3.3.2"
            details.because "https://github.com/micronaut-projects/micronaut-servlet/pull/354"
        }
    }
}
